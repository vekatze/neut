(no-implicit-core)

(include "core/notation.neut")

(include "core/functor.neut")

(include "core/monad.neut")

(section coproduct)

(inductive coproduct
  ((e tau)
   (a tau))
  (left ((_ e))
    (coproduct e a))
  (right ((_ a))
    (coproduct e a)))

(define map
  ((e tau)
   (a tau)
   (b tau)
   (f (hom a b))
   (m (coproduct e a)))
  (witness (coproduct e b)
    (coproduct.case (coproduct e b) * * m
      (lambda (err)
        (left * * err))
      (lambda (val)
        (right * * (f val))))))

(define return
  ((e tau)
   (a tau)
   (x a))
  (witness (coproduct e a)
    (right * * x)))

(define bind
  ((e tau)
   (a tau)
   (b tau)
   (comp (coproduct e a))
   (k (hom a (coproduct e b))))
  (witness (coproduct e b)
    (coproduct.case (coproduct e b) * * comp
      (lambda (err)
        (left * * err))
      (lambda (val)
        (k val)))))

(define throw
  ((e tau)
   (a tau)
   (err e))
  (witness (coproduct e a)
    (left * * err)))

(define witness-functor
  ((e tau))
  (witness (functor (coproduct e _))
    (functor.new
      (coproduct e _)
      (coproduct.map e _ _ _ _))))

(define witness-monad
  ((e tau))
  (witness (monad (coproduct e _))
    (monad.new
      (coproduct e _)
      (witness-functor e)
      (coproduct.return e _ _)
      (coproduct.bind e _ _ _ _))))

(end coproduct)
