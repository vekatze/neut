import {
  sero.common.get {get-int64},
  sero.common.put {put-int64},
  sero.sero {Sero, sero},
  this.language.common.module-digest {Module-Digest, module-digest, module-digest-sero, text-from-module-digest},
}

data module-id {
| Main
| Base
| Library(module-digest)
}

define text-from-module-id(i: &module-id): &text {
  case i {
  | Main =>
    "this"
  | Library(digest) =>
    text-from-module-digest(digest)
  | Base =>
    "base"
  }
}

inline module-id-ord: ord(module-id) {
  Ord of {
    compare := {
      function (x, y) {
        case x, y {
        | Main, Main =>
          EQ
        | Main, Base =>
          LT
        | Main, Library(_) =>
          LT
        | Base, Main =>
          GT
        | Base, Base =>
          EQ
        | Base, Library(_) =>
          LT
        | Library(_), Main =>
          GT
        | Library(_), Base =>
          GT
        | Library(Module-Digest(d1)), Library(Module-Digest(d2)) =>
          let Ord of {compare} = core.text.ord.as-ord;
          compare(d1, d2)
        }
      }
    },
  }
}

inline module-id-sero: sero(module-id) {
  Sero of {
    put := {
      function (k, v) {
        case v {
        | Main =>
          put-int64(k, 0);
        | Library(digest) =>
          let Sero of {put} = module-digest-sero;
          put-int64(k, 1);
          put(k, digest);
        | Base =>
          put-int64(k, 2)
        }
      }
    },
    get := {
      function (k) {
        try tag = get-int64(k);
        match tag {
        | 0 =>
          Right(box {Main})
        | 1 =>
          let Sero of {get} = module-digest-sero;
          try digest = get(k);
          Right(box {
            letbox digest = digest;
            Library(digest)
          })
        | 2 =>
          Right(box {Base})
        | _ =>
          none
        }
      }
    },
  }
}
