import {
  core.ansi {
    Background,
    Blue,
    Bold,
    Color,
    Color-16,
    Foreground,
    Italic,
    Normal,
    Red,
    Set-Style,
    Vivid,
    style,
  },
  core.list {for-N},
  core.text-builder {append-ansi, append-text, extract, make-text-builder},
}

data color-text {
| Empty
| Join(list(style), text, color-text)
}

define packed(ts: list(style), t: text): color-text {
  Join(ts, t, Empty)
}

define packed'(t: text): color-text {
  Join(Nil, t, Empty)
}

define _unpacked(t: &color-text): text {
  let b = make-text-builder(64);
  let _ on b = {
    let f =
      define loop(t: &color-text): unit {
        case t {
        | Empty =>
          Unit
        | Join(ts, t, rest) =>
          for-N(ts, function (t) {
            append-ansi(b, Set-Style(*t));
          });
          append-text(b, t);
          append-ansi(b, Set-Style(Normal));
          loop(rest)
        }
      };
    f(t)
  };
  extract(b)
}

define _unpacked-uncolored(t: &color-text): text {
  let b = make-text-builder(256);
  let _ on b = {
    let f =
      define loop(t: &color-text): unit {
        case t {
        | Empty =>
          Unit
        | Join(_, t, rest) =>
          append-text(b, t);
          loop(rest)
        }
      };
    f(t)
  };
  extract(b)
}

define join(t1: color-text, t2: color-text): color-text {
  match t1 {
  | Empty =>
    t2
  | Join(tones, content, rest) =>
    Join(tones, content, join(rest, t2))
  }
}

define zen(): unit {
  pin t =
    Join(
      List[Color(Foreground, Color-16(Vivid, Blue)), Bold],
      *"hello",
      Join(List[], *"\n", Empty),
    );
  pin fragment1 = _unpacked(t);
  pin fragment2 = _unpacked-uncolored(t);
  pin t' = packed(List[Color(Background, Color-16(Vivid, Red)), Italic], *"sample\n");
  pin fragment3 = _unpacked(t');
  print(fragment1);
  print(fragment2);
  print(fragment3);
  print("sample\n")
}
