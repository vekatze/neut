import {
  core.ansi {Blue, Bold, Color, Color-16, Foreground, Vivid},
  core.file.descriptor {descriptor, stderr, stdout},
  core.text {_get-content-pointer, text-byte-length},
  this.color.color-kit {_Color-Kit, color-kit, make-color-kit},
  this.color.text {_unpacked, _unpacked-uncolored, color-text, packed},
}

foreign {
  dprintf(int, pointer): void,
}

define _print(d: descriptor, t: &text): unit {
  let fmt = _get-content-pointer("%.*s\0");
  let len = text-byte-length(t);
  let val = _get-content-pointer(t);
  let _ = magic external dprintf(magic cast(descriptor, int, d), fmt)(len: int, val: pointer);
  Unit
}

define print-stdout(k: &color-kit, t: &color-text): unit {
  tie _Color-Kit of {colorize-stdout} = k;
  pin t' =
    if *colorize-stdout {
      _unpacked(t)
    } else {
      _unpacked-uncolored(t)
    };
  _print(stdout, t')
}

define print-stderr(k: &color-kit, t: &color-text): unit {
  tie _Color-Kit of {colorize-stderr} = k;
  pin t' =
    if *colorize-stderr {
      _unpacked(t)
    } else {
      _unpacked-uncolored(t)
    };
  _print(stderr, t')
}

define zen(): unit {
  pin t' = packed(List[Color(Foreground, Color-16(Vivid, Blue)), Bold], *"sample\n");
  pin h = make-color-kit(False, True);
  print-stdout(h, t');
  print-stderr(h, t')
}
