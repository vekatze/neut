import {
  core.cell {make-cell},
  core.dictionary {dictionary},
  this.kernel.common.optimizable-data {optimizable-data},
  this.language.common.definite-description {definite-description, ord-dd},
}

data optimizable-data-kit {
| _Optimizable-Data-Kit(
    opt-data-map-ref: cell(dictionary(definite-description, optimizable-data)),
  )
}

define make-optimizable-data-kit(): optimizable-data-kit {
  let opt-data-map-ref = make-cell(box {core.dictionary.empty});
  _Optimizable-Data-Kit of {opt-data-map-ref}
}

define insert(k: &optimizable-data-kit, dd: definite-description, od: optimizable-data): unit {
  tie _Optimizable-Data-Kit(ref) = k;
  core.cell.mutate(ref, function (dict) {
    box {
      letbox dict = dict;
      letbox dd = quote {dd};
      letbox od = quote {od};
      core.dictionary.make-insert(ord-dd)(dict, dd, od)
    }
  })
}

define lookup(k: &optimizable-data-kit, dd: definite-description): ?optimizable-data {
  tie _Optimizable-Data-Kit(ref) = k;
  core.cell.borrow(ref, function (dict) {
    pin dd = dd;
    match core.dictionary.make-lookup(ord-dd)(dict, dd) {
    | Left(_) =>
      quote {none}
    | Right(v) =>
      quote {Right(*v)}
    }
  })
}
