import {
  core.cell {clone, make-cell},
  core.dictionary {make-lookup},
  core.text-builder {append-text, extract, make-text-builder},
  path.path {split-extension, take-last},
  this.app.app {app},
  this.app.run {raise-error'},
  this.kernel.common.clang-option {clang-option},
  this.kernel.common.kit.global.platform {get-clang-digest, platform-kit},
  this.kernel.common.module {Module, extract-module, main-module, module},
  this.kernel.common.target {Main, Named, Peripheral, Peripheral-Single, Target-Summary, Zen, target},
  this.language.common.module-id {Library},
  this.logger.logger-kit {logger-kit},
}

data path-kit {
| _Path-Kit(
    main-module: &main-module,
    cache-cell: cell(?text),
    logger-kit: &logger-kit,
    platform-kit: &platform-kit,
  )
}

define make-path-kit(mm: &main-module, lk: &logger-kit, pk: &platform-kit): path-kit {
  let cache-cell = make-cell(box {none});
  _Path-Kit(mm, cache-cell, lk, pk)
}

define get-base-name(path: &text): text {
  pin bar = take-last(path);
  let Pair(buz, _) = split-extension(bar);
  buz
}

define ensure-not-in-dependency-dir(m: &main-module): app(unit) {
  tie Module of {id} = extract-module(m);
  case id {
  | Library(_) =>
    raise-error'(*"This command cannot be used under a dependency directory")
  | _ =>
    Right(Unit)
  }
}

define get-clang-option(t: &target, m: &module): app(clang-option) {
  case t {
  | Main(main-module) =>
    case main-module {
    | Named(name, _) =>
      tie Module of {target} = m;
      let lookup = make-lookup(core.text.ord.as-ord);
      match lookup(target, name) {
      | Left(_) =>
        let buf = make-text-builder(64);
        let _ on buf = {
          append-text(buf, "No such target is defined: `");
          append-text(buf, name);
          append-text(buf, "`");
        };
        raise-error'(extract(buf))
      | Right(v) =>
        tie Target-Summary of {clang-option} = v;
        Right(*clang-option)
      }
    | Zen(_, clang-option) =>
      Right(*clang-option)
    }
  | Peripheral =>
    Right(this.kernel.common.clang-option.empty-clang-option)
  | Peripheral-Single(_) =>
    Right(this.kernel.common.clang-option.empty-clang-option)
  }
}

define zen(): unit {
  pin x = get-base-name("foo/bar/buz.nt");
  vet(x);
}
