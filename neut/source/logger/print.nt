import {
  core.ansi {Black, Color, Color-16, Foreground, Vivid},
  core.list {fold-right-N},
  this.color.make-color-kit {make-color-kit},
  this.color.print {print-stderr, print-stdout},
  this.color.text {join, packed, packed'},
  this.logger.hint {new-hint},
  this.logger.log {Log, _show-content, _show-level, _show-position, log},
  this.logger.log-level {Note, Warning, log-level},
  this.logger.logger-kit {_Logger-Kit, _show-time, logger-kit},
  this.logger.make-logger-kit {make-logger-kit},
  time.time {get-time, sub-time},
}

define print-log(k: &logger-kit, l: &log): unit {
  tie _Logger-Kit of {color-kit} = k;
  tie Log of {position, level, content} = l;
  let position-text = _show-position(position);
  let level-text = _show-level(*level);
  let content-text = _show-content of {level := *level, content := *content};
  pin log-text = join(position-text, join(level-text, content-text));
  print-stdout(color-kit, log-text)
}

define print-log-error(k: &logger-kit, l: &log): unit {
  tie _Logger-Kit of {color-kit} = k;
  tie Log of {position, level, content} = l;
  let position-text = _show-position(position);
  let level-text = _show-level(*level);
  let content-text = _show-content of {level := *level, content := *content};
  pin log-text = join(position-text, join(level-text, content-text));
  print-stderr(color-kit, log-text)
}

define print-log-list(k: &logger-kit, ls: &list(log)): unit {
  fold-right-N(ls, Unit, function (l, _) {
    print-log(k, l)
  })
}

define print-log-error-list(k: &logger-kit, ls: &list(log)): unit {
  fold-right-N(ls, Unit, function (l, _) {
    print-log-error(k, l)
  })
}

define _print-log-without-position(k: &logger-kit, l: log-level, content: text): unit {
  pin l = Log of {position := none, level := l, content};
  print-log(k, l)
}

define print-note(k: &logger-kit, content: text): unit {
  _print-log-without-position(k, Note, content)
}

define print-warning(k: &logger-kit, content: text): unit {
  _print-log-without-position(k, Warning, content)
}

define report(k: &logger-kit, message: text): unit {
  tie _Logger-Kit of {color-kit, enable-debug-mode, base-time} = k;
  when *enable-debug-mode {
    let current-time = get-time();
    let elapsed-time = sub-time(current-time, *base-time);
    let time-text = packed(List[Color(Foreground, Color-16(Vivid, Black))], _show-time(elapsed-time));
    pin debug-text = join(time-text, join(packed'(*" "), join(packed'(message), packed'(*"\n"))));
    print-stderr(color-kit, debug-text)
  }
}

define zen(): unit {
  let color-kit = make-color-kit(True, True);
  pin k = make-logger-kit(color-kit, True);
  let m = new-hint(12, 3456, "/Users/foo/path/to/file.nt");
  pin l =
    Log of {
      position := Right(m),
      level := Note,
      content := *"Lorem ipsum.\nProin non hendrerit neque, id rhoncus nisi.",
    };
  print-log(k, l);
  print-warning(k, *"Nullam non dui a felis viverra molestie eget id mauris.");
  print-note(k, *"Aenean tortor ligula, efficitur id viverra ac, mattis in orci.\nDonec mattis ex quis scelerisque faucibus.");
  report(k, *"Donec sit amet euismod felis.\nVivamus ac lectus fermentum, blandit purus at, consectetur orci.")
}
