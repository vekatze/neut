import {
  core.ansi {Bold},
  core.list {for-each-N},
  this.color.text {Empty, color-text, join, packed, packed'},
  this.logger.hint {hint, show-file-pos},
  this.logger.log-level {_log-level-to-pad, _log-level-to-text, _log-level-to-tone, log-level},
}

data log {
| Log(
    position: ?hint,
    level: log-level,
    content: text,
  )
}

define _show-position(h: &?hint): color-text {
  case h {
  | Left(_) =>
    Empty
  | Right(h) =>
    join(packed(List[Bold], show-file-pos(h)), packed'(*"\n"))
  }
}

define _show-level(l: log-level): color-text {
  packed(_log-level-to-tone(l), core.text.append(_log-level-to-text(l), ": "))
}

define _show-content(level: log-level, content: text): color-text {
  pin pad = _log-level-to-pad(level);
  pin adjusted-content = {
    let lines on content = core.text.split(content, "\n");
    match lines {
    | Nil =>
      content
    | Cons(head, tail) =>
      pin tail = tail;
      let tail' =
        for-each-N(tail, function (t) {
          core.text.append(pad, t)
        });
      core.text.intercalate(Cons(head, tail'), *"\n")
    }
  };
  packed'(core.text.append(adjusted-content, "\n"))
}
