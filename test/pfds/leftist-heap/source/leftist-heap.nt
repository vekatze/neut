import {
  core.int.show {show-int},
}

// leftist heap
data heap(a) {
| Leaf
  // int stands for the rank of the Node
| Node(int, a, heap(a), heap(a))
}

data heap-signature(a) {
| Heap(
    empty: heap(a),
    is-empty: (&heap(a)) -> bool,
    insert: (a, heap(a)) -> heap(a),
    merge: (heap(a), heap(a)) -> heap(a),
    find-min: (&heap(a)) -> ?a,
    delete-min: (heap(a)) -> ?heap(a),
  )
}

define make-heap-signature(a: type, !compare: (a, a) -> ordering): heap-signature(a) {
  let !merge =
    let !rank =
      function (h: &heap(a)) {
        case h {
        | Leaf =>
          0
        | Node(r, _, _, _) =>
          *r
        }
      }
    in
    let !make-Node =
      function (x: a, l: heap(a), r: heap(a)) {
        let rank1 on l = !rank(l) in
        let rank2 on r = !rank(r) in
        if ge-int(rank1, rank2) {
          Node(add-int(rank2, 1), x, l, r)
        } else {
          Node(add-int(rank1, 1), x, r, l)
        }
      }
    in
    define merge(h1: heap(a), h2: heap(a)): heap(a) {
      match h1, h2 {
      | h1, Leaf =>
        h1
      | Leaf, h2 =>
        h2
      | Node(rank1, !value1, left1, right1), Node(rank2, !value2, left2, right2) =>
        match !compare(!value1, !value2) {
        | LT =>
          !make-Node(!value1, left1, merge(right1, Node(rank2, !value2, left2, right2)))
        | _ =>
          !make-Node(!value2, left2, merge(Node(rank1, !value1, left1, right1), right2))
        }
      }
    }
  in
  Heap of {
    empty = Leaf,
    is-empty = {
      function (h: &heap(a)) {
        case h {
        | Leaf =>
          True
        | Node(_, _, _, _) =>
          False
        }
      }
    },
    insert = {
      function (x: a, h: heap(a)) {
        !merge(Node(1, x, Leaf, Leaf), h)
      }
    },
    merge = !merge,
    find-min = {
      function (h: &heap(a)) {
        case h {
        | Leaf =>
          none
        | Node(_, value, _, _) =>
          Right(*value)
        }
      }
    },
    delete-min = {
      function (h: heap(a)) {
        match h {
        | Leaf =>
          none
        | Node(_, _, left, right) =>
          Right(!merge(left, right))
        }
      }
    },
  }
}

define compare-int(x: int, y: int): ordering {
  if lt-int(x, y) {
    LT
  } else-if gt-int(x, y) {
    GT
  } else {
    EQ
  }
}

define main(): unit {
  let int-heap-signature = make-heap-signature(_, compare-int) in
  let Heap of {empty, insert = !insert, delete-min, find-min} = int-heap-signature in
  let h = empty in
  let h = !insert(10, h) in
  let h = !insert(2, h) in
  let h = !insert(5, h) in
  let h = !insert(8, h) in
  let h = !insert(1, h) in
  let h = !insert(3, h) in
  let mh = delete-min(h) in
  match mh {
  | Left(_) =>
    printf("{}\n", [show-int(100)])
  | Right(h) =>
    let h = !insert(9, h) in
    let min on h =
      match find-min(h) {
      | Left(_) =>
        printf("{}\n", [show-int(100)])
      | Right(value) =>
        printf("{}\n", [show-int(value)])
      }
    in
    let _ = h in
    let _ = min in
    Unit
  }
}
