import {
  core.int.ord,
  core.int.show {show-int},
}

data set(a) {
| Leaf
| Node(set(a), a, set(a))
}

data set-signature(a) {
| Set(empty: set(a), insert: (a, set(a)) -> set(a), member: (a, &set(a)) -> bool)
}

define make-set-signature(a: type, !compare: (a, a) -> ordering): set-signature(a) {
  Set of {
    empty = Leaf,
    insert = {
      define insert(!new-value: a, haystack: set(a)): set(a) {
        match haystack {
        | Leaf =>
          Node(Leaf, !new-value, Leaf)
        | Node(left, !x, right) =>
          match !compare(!new-value, !x) {
          | LT =>
            Node(insert(!new-value, left), !x, right)
          | EQ =>
            Node(left, !x, right)
          | GT =>
            Node(left, !x, insert(!new-value, right))
          }
        }
      }
    },
    member = {
      define member(!needle: a, haystack: &set(a)) {
        case haystack {
        | Leaf =>
          False
        | Node(left, x, right) =>
          match !compare(!needle, *x) {
          | LT =>
            member(!needle, left)
          | EQ =>
            True
          | GT =>
            member(!needle, right)
          }
        }
      }
    },
  }
}

define main(): unit {
  let Ord of {compare} = core.int.ord.as-ord in
  let int-set-signature = make-set-signature(int, compare) in
  match int-set-signature {
  | Set of {insert = !insert, member, empty} =>
    let my-set = !insert(10, empty) in
    let my-set = !insert(20, my-set) in
    let my-set = !insert(20, my-set) in
    let my-set = !insert(20, my-set) in
    let my-set = !insert(30, my-set) in
    let result on my-set = member(5, my-set) in
    let _ = my-set in
    if result {
      printf("{}\n", [show-int(1)])
    } else {
      printf("{}\n", [show-int(2)])
    }
  }
}
