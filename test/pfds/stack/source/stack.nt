import {
  core.bool {False, True, bool},
  core.except {OK, none},
  core.text,
  core.text.io {print, print-int},
  core.unit {unit},
}

data stack(a) {
| Nil
| Cons(a, stack(a))
}

define empty(a: type): stack(a) {
  Nil
}

define is-empty(a: type, xs: &stack(a)): bool {
  case xs {
  | Nil =>
    True
  | Cons(_, _) =>
    False
  }
}

define head(a: type, xs: &stack(a)): ?&a {
  case xs {
  | Nil =>
    none
  | Cons(a, _) =>
    OK(a)
  }
}

define tail(a: type, xs: &stack(a)): ?&stack(a) {
  case xs {
  | Nil =>
    none
  | Cons(_, rest) =>
    OK(rest)
  }
}

define update(a: type, xs: stack(a), index: int, value: a): stack(a) {
  match xs {
  | Nil =>
    Nil
  | Cons(y, ys) =>
    if eq-int(index, 0) {
      Cons(value, ys)
    } else {
      Cons(y, update(a, ys, sub-int(index, 1), value))
    }
  }
}

define suffixes(a: type, !xs: stack(a)): stack(stack(a)) {
  match !xs {
  | Nil =>
    Nil
  | Cons(_, ys) =>
    Cons(!xs, suffixes(a, ys))
  }
}

define suffixes-noetic(a: type, xs: &stack(a)): stack(&stack(a)) {
  case xs {
  | Nil =>
    Nil
  | Cons(_, ys) =>
    Cons(xs, suffixes-noetic(a, ys))
  }
}

define main(): unit {
  let !xs = empty(int) in
  let ys = Cons(10, !xs) in
  let zs = Cons(30, Cons(20, ys)) in
  let !zs = update(_, zs, 1, 20000) in
  let _ = suffixes(_, !zs) in
  let calc on !zs, !xs =
    let b = is-empty(_, !zs) in
    let _ = is-empty(_, !xs) in
    let _ = head(_, !zs) in
    let _ = suffixes-noetic(_, !zs) in
    let _ = head(_, !xs) in
    let _ = tail(_, !zs) in
    let _ = tail(_, !zs) in
    if b {
      1
    } else {
      2
    }
  in
  let _ = !zs in
  let _ = !xs in
  print-int(calc);
  print("\n")
}
