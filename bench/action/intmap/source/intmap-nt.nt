import {
  IntMap {insert, intmap},
  core.control {grow},
  core.int.parse {parse-int},
  core.intmap {lookup},
  core.random {random-int},
  core.system {get-argv},
  core.text.io {print-int},
}

define make-big-dict(size: int): intmap(int) {
  grow(IntMap.empty, size, function (acc, _) {
    let key = random-int(1000000) in
    let val = random-int(1000000) in
    insert(acc, key, val)
  })
}

define random-lookup-sum(count: int, d: &intmap(int)): unit {
  let rsum =
    grow(0, count, function (acc, _) {
      let key = random-int(1000000) in
      let val on key =
        match lookup(d, *key) {
        | Error(_) =>
          0
        | OK(v) =>
          *v
        }
      in
      let _ = key in
      add-int(acc, val)
    })
  in
  print-int(rsum)
}

define main(): unit {
  let args = get-argv() in
  match args {
  | [_, size-str] =>
    let _ on size-str =
      match parse-int(size-str) {
      | OK(size) =>
        let big-dict = make-big-dict(size) in
        let _ on big-dict = random-lookup-sum(size, big-dict) in
        let _ = big-dict in
        Unit
      | Error(_) =>
        printf("couldn't parse `{}` as an integer\n", [*size-str])
      }
    in
    let _ = size-str in
    Unit
  | _ =>
    print("usage: intmap-nt SIZE\n")
  }
}
